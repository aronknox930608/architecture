apply plugin: 'java'
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

configurations {
    injar
    libjar
}

String getClassesJar(String platform) {
    def javaHome = System.properties.'java.home'
    switch (platform.replaceAll(' ', '').toLowerCase()) {
        case ~/.*osx.*/:
            return "$javaHome/../Classes/classes.jar"
        case ~/.*linux.*/:
        case ~/.*darwin.*/:
        case ~/.*win.*/:
            return "$javaHome/lib/rt.jar"
        default: throw new GradleException('unable to determine java runtime classes!')
    }
}

def guavaVersion = '18.0'
def classesJar = files(getClassesJar(System.properties['os.name']))

//noinspection GradleImproperProjectLevelStatement
dependencies {
    injar "com.google.guava:guava:${guavaVersion}"
    libjar 'com.google.code.findbugs:jsr305:3.0.0'
    libjar classesJar
}

/**
 * Run proguard to shrink guava to only include the stuff we specify and its dependencies.
 */
task createCustomGuava(type: proguard.gradle.ProGuardTask) {
    injars configurations.injar.files
    libraryjars configurations.libjar.files
    outjars file("libs/guava_${guavaVersion}_base.jar")
    dontoptimize
    dontobfuscate
    keep '''public class com.google.common.base.** {
        public *;
    }'''
    keep '''public class com.google.common.collect.** {
        public *;
    }'''
    keep '''public class com.google.common.reflect.** {
        public *;
    }'''
}
